module

public import Neighborhood.Semantics.Completeness

/-!
# Axiom `N` on neighborhood frames

The frame condition corresponding to the axiom `N := □⊤`: the whole carrier is a neighborhood of
every world. This is exactly the requirement that `N` be valid on the frame.
-/

@[expose] public section

variable {κ : Type u} [Nonempty κ] {α : Type v} {F : Frame κ}

/-- A frame contains its unit: the whole carrier is a neighborhood of every world. -/
class Frame.ContainsUnit (F : Frame κ) : Prop where
  contains_unit : F.box Set.univ = Set.univ

lemma Frame.contains_unit [F.ContainsUnit] : F.box Set.univ = Set.univ :=
  Frame.ContainsUnit.contains_unit

@[simp]
lemma Frame.univ_mem [F.ContainsUnit] (x : F.World) : Set.univ ∈ F.𝒩 x := by
  have : x ∈ F.box Set.univ := by rw [F.contains_unit]; trivial
  simpa [Frame.box] using this

@[simp, grind .]
theorem valid_axiomN_of_containsUnit [F.ContainsUnit] : F ⊧ (Axioms.N : Formula α) := by
  intro V x
  simp [Forces, F.contains_unit]

theorem containsUnit_of_valid_axiomN (h : F ⊧ (Axioms.N : Formula α)) : F.ContainsUnit := by
  constructor
  ext x
  simpa [Forces] using h (fun _ => Set.univ) x

section

variable {α : Type u} {L : Logic α} [DecidableEq α] [L.Cl] [L.HasRE] [Nonempty (MaximalConsistentSet L)]

instance [L.HasAxiomN] : (basicCanonicalModel L).ContainsUnit := by
  constructor
  ext Ω
  apply iff_of_true _ (Set.mem_univ Ω)
  exact ⟨⊤, MaximalConsistentSet.mem_of_prove (by simp), by simp⟩

instance {P : MaximalConsistentSet L → Set (Proofset L)} [L.HasAxiomN] :
    (relativeBasicCanonicalModel L P).ContainsUnit := by
  constructor
  ext Ω
  apply iff_of_true _ (Set.mem_univ Ω)
  exact Or.inl ⟨⊤, MaximalConsistentSet.mem_of_prove (by simp), by simp⟩

/-- Direct instance on the abbrev head: under Lean v4.33, instance search no longer unfolds
`intermediateRelativeMaximalCanonicalModel` (an abbrev of `relativeBasicCanonicalModel`) to match
the general instance above. -/
instance [L.HasAxiomN] :
    (intermediateRelativeMaximalCanonicalModel L).ContainsUnit := by
  constructor
  ext Ω
  apply iff_of_true _ (Set.mem_univ Ω)
  exact Or.inl ⟨⊤, MaximalConsistentSet.mem_of_prove (by simp), by simp⟩

end

end
